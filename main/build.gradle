if (isBuildMainApp.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        if (isBuildMainApp.toBoolean()) {
            applicationId "com.heng.main"
        }
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        //ARouter配置
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
        
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (isBuildMainApp.toBoolean()) {
                manifest.srcFile 'src/main/buildApp/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/buildModule/AndroidManifest.xml'
                kotlin {
                    exclude 'debug/**'
                }
            }
        }
    }

    /*设置资源前缀*/
    resourcePrefix "main_"

    compileOptions {
        sourceCompatibility = rootProject.ext.sourceCompatibility
        targetCompatibility = rootProject.ext.targetCompatibility
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlinStdLib_JDK7
    implementation deps.appcompat
    implementation deps.design
    implementation deps.coreKtx
    implementation deps.constraintLayout
    implementation deps.legacySupportV4

    implementation deps.arouterApi
    kapt deps.arouterCompiler

    implementation deps.retrofitConvertGson

    implementation deps.kotlinxCoroutinesCore
    implementation deps.kotlinxCoroutinesAndroid
    implementation deps.retrofit2KotlinCoroutinesAdapter

    implementation deps.multidex

    implementation deps.roomRuntime
    implementation deps.roomRxjava2
    kapt           deps.roomCompiler

    testImplementation deps.roomTest

    testImplementation deps.junit
    androidTestImplementation deps.extJunit
    androidTestImplementation deps.espressoCore

    implementation project(path: ':common')
    implementation project(path: ':dataStore')

    if (!isBuildHomeApp.toBoolean()) {
        implementation project(path: ':home')
    }

    if (!isBuildVideoApp.toBoolean()) {
        implementation project(path: ':video')
    }

    if (!isBuildPersonApp.toBoolean()) {
        implementation project(path: ':person')
    }
}
