if (isMainAppDebug.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        if (isMainAppDebug.toBoolean()) {
            applicationId "com.heng.main"
        }
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        //ARouter配置
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (isMainAppDebug.toBoolean()) {
                manifest.srcFile 'src/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'

                java {
                    exclude 'debug/**'
                }

                kotlin {
                    exclude 'debug/**'
                }
            }
        }
    }

    /*设置资源前缀*/
    resourcePrefix "main_"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlinStdLib_JDK7
    implementation deps.appcompat
    implementation deps.design
    implementation deps.coreKtx
    implementation deps.constraintLayout
    implementation deps.legacySupportV4
    implementation deps.arouterApi
    kapt deps.arouterCompiler
    testImplementation deps.junit
    androidTestImplementation deps.extJunit
    androidTestImplementation deps.espressoCore
    implementation project(path: ':common')
    if (!isHomeDebug.toBoolean()) {
        implementation project(path: ':home')
    }
    if (!isVideoDebug.toBoolean()) {
        implementation project(path: ':video')
    }
    if (!isPersonDebug.toBoolean()) {
        implementation project(path: ':person')
    }
}
